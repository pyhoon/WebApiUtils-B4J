AppType=StandardJava
Build1=Default,b4j.webapi,SQLite,hu2_acceptall
Build2=MariaDB,b4j.webapi,MariaDB,hu2_acceptall
Build3=MySQL,b4j.webapi,MySQL,hu2_acceptall
File1=category.html
File2=config.example
File3=help.html
File4=index.html
File5=main.html
File6=mariadb.example
File7=mysql.example
File8=sqlite.example
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
Group=App
Library1=jcore
Library2=json
Library3=jstringutils
Library4=miniormutils
Library5=xml2map
Library6=endsmeet
Module1=CategoriesApiHandler
Module10=WebApiUtils
Module2=CategoriesWebHandler
Module3=FindApiHandler
Module4=HelpHandler
Module5=HttpsFilter
Module6=IndexWebHandler
Module7=JavaScript
Module8=ProductsApiHandler
Module9=UsersApiHandler
NumberOfFiles=8
NumberOfLibraries=6
NumberOfModules=10
Version=10.3
@EndOfDesignText@
#Region Project Attributes
' Product: WebApiUtils
' Version: 5.60
' License: MIT License
' Developer: Poon Yip Hoon (Aeric)
#CommandLineArgs:
#MergeLibraries: True
#End Region
#If SQLite
#AdditionalJar: sqlite-jdbc-3.7.2
#Else If MySQL
#AdditionalJar: mysql-connector-j-9.3.0
#Else If MariaDB
#AdditionalJar: mariadb-java-client-3.5.6
#End If
#Region Macros
#Macro: Title, Update Manifest, ide://run?file=%JAVABIN%\java.exe&Args=-jar&Args=%ADDITIONAL%\..\B4X\manifest-writer.jar&Args=%PROJECT%&Args=%PROJECT%&Args=Version&Args=5.60
#Macro: Title, Create B4xLib, ide://run?file=%JAVABIN%\jar.exe&WorkingDirectory=..&Args=-cMf&Args=..\release\%PROJECT_NAME%.b4xlib&Args=Snippets&Args=%PROJECT_NAME%.bas&Args=manifest.txt&Args=LICENSE
#Macro: Title, Release folder, ide://run?file=%WINDIR%\SysWOW64\explorer.exe&Args=%PROJECT%\..\release
#Macro: Title, Copy to AddLibs, ide://run?file=%COMSPEC%&Args=/c&Args=copy&Args=%PROJECT%\..\release\*.b4xlib&Args=%ADDITIONAL%
'#Macro: Title, GetLibraries, ide://run?file=%JAVABIN%\java.exe&args=-jar&args=%ADDITIONAL%\..\B4X\libget.jar&args=%PROJECT%&args=false
'#Macro: Title, GetResources, ide://run?file=%JAVABIN%\java.exe&args=-jar&args=%ADDITIONAL%\..\B4X\resget.jar&args=%PROJECT%&args=true
#End Region

Sub Process_Globals
	Public DB 					As MiniORM
	Public MS 					As ORMSettings
	Public App 					As EndsMeet
	Public Api 					As ApiSettings
	Public Const COLOR_RED 		As Int = -65536
	Public Const COLOR_BLUE 	As Int = -16776961
	Public Const VERSION 		As String = "5.60"
End Sub

' <link>Open in browser|http://127.0.0.1:8080</link>
' <link>Donate|https://paypal.me/aeric80/</link> | <link>Forum|https://www.b4x.com/android/forum/threads/web-webapiutils-v5-50.167012/)</link> | <link>GitHub|https://github.com/pyhoon/WebApiUtils-B4J</link>
Sub AppStart (Args() As String)
	App.Initialize
	App.LoadConfig
	App.LogEnabled = True
	#If Release
	App.ssl.Enabled = True
	App.RedirectToHttps = True
	#End If
	
	' *** Api Settings ***
	Api = App.api
	'Api.OrderedKeys = False
	'Api.PayloadType = WebApiUtils.MIME_TYPE_JSON
	'Api.ContentType = WebApiUtils.MIME_TYPE_XML
	#If Debug
	Api.EnableHelp = True
	#End If

	' *** Web handlers ***
	App.Get("", "IndexWebHandler")
	#If Debug
	App.Get("/help", "HelpHandler")
	#End If
	App.Get("/categories", "CategoriesWebHandler")
	'App.Get("/users", "UsersWebHandler")
	
	' *** Api handlers ***
	App.Get("/api/categories", "CategoriesApiHandler")
	App.Get("/api/categories/*", "CategoriesApiHandler")
	App.Post("/api/categories", "CategoriesApiHandler")
	App.Put("/api/categories/*", "CategoriesApiHandler")
	App.Delete("/api/categories/*", "CategoriesApiHandler")
	App.Get("/api/products", "ProductsApiHandler")
	App.Get("/api/products/*", "ProductsApiHandler")
	App.Post("/api/products", "ProductsApiHandler")
	App.Put("/api/products/*", "ProductsApiHandler")
	App.Delete("/api/products/*", "ProductsApiHandler")
	App.Get("/api/find", "FindApiHandler")
	App.Get("/api/find/products-by-category_id/*", "FindApiHandler")
	App.Post("/api/find", "FindApiHandler")
	
	App.Get("/api/users", "UsersApiHandler")
	App.Get("/api/users/*", "UsersApiHandler")
	App.Post("/api/users", "UsersApiHandler")
	App.Put("/api/users/*", "UsersApiHandler")
	App.Delete("/api/users/*", "UsersApiHandler")

	App.Start
	App.LogStartupMessage
	App.ctx.Put("VERSION", VERSION)
	Log($"App version: ${VERSION}"$)
	Log($"Open the following URL from your web browser"$)
	Log(App.ServerUrl)
	ConfigureDatabase
	JavaScript.CreateJSFiles
	StartMessageLoop
End Sub

' Set global ApiSettings from Main to HttpResponseMessage
Public Sub SetApiMessage (Message As HttpResponseMessage)
	Message.VerboseMode = Api.VerboseMode
	Message.OrderedKeys = Api.OrderedKeys
	Message.ContentType = Api.ContentType
	Message.PayloadType = Api.PayloadType
End Sub

Public Sub ConfigureDatabase
	Try
		#If MariaDB
		Dim dbvar As String = "mariadb"
		#Else If MySQL
		Dim dbvar As String = "mysql"
		#Else
		Dim dbvar As String = "sqlite"
		#End If
		If File.Exists(File.DirApp, $"${dbvar}.ini"$) = False Then
			File.Copy(File.DirAssets, $"${dbvar}.example"$, File.DirApp, $"${dbvar}.ini"$)
		End If
		Dim ctx As Map = File.ReadMap(File.DirApp, $"${dbvar}.ini"$)
		DB.Initialize
		DB.ShowExtraLogs = True
		MS.Initialize
		MS.DBType = ctx.GetDefault("DbType", "")
		Select MS.DBType
			Case DB.MARIADB, DB.MYSQL
				MS.DBHost = ctx.GetDefault("DbHost", "")
				MS.DBPort = ctx.GetDefault("DbPort", "")
				MS.DBName = ctx.GetDefault("DbName", "")
				MS.DriverClass = ctx.GetDefault("DriverClass", "")
				MS.JdbcUrl = ctx.GetDefault("JdbcUrl", "")
				MS.User = ctx.GetDefault("User", "")
				MS.Password = ctx.GetDefault("Password", "")
				MS.MaxPoolSize = ctx.GetDefault("MaxPoolSize", 0)
			Case DB.SQLITE
				MS.DBDir = ctx.GetDefault("DbDir", File.DirApp)
				MS.DBFile = ctx.GetDefault("DbFile", "Pakai.db")
				MS.JournalMode = "WAL"
			Case Else
				LogColor($"${MS.DBType} not supported!"$, COLOR_RED)
				Log("Application is terminated.")
				ExitApplication
		End Select
		DB.Settings = MS

		LogColor("Checking database...", COLOR_BLUE)
		Select DB.DbType
			Case DB.SQLITE
				Dim DBExist As Boolean = DB.Exist
			Case DB.MARIADB, DB.MYSQL
				Wait For (DB.InitSchema) Complete (Success As Boolean)
				If Success = False Then
					LogColor("Database initilialization failed!", COLOR_RED)
					Log("Application is terminated.")
					ExitApplication
				End If
				If DB.Test = False Then
					LogColor("Database connection test failed!", COLOR_RED)
					Log("Application is terminated.")
					ExitApplication
				End If
				Wait For (DB.ExistAsync) Complete (DBExist As Boolean)
			Case Else
				LogColor("Database type is unknown!", COLOR_RED)
				ExitApplication
		End Select
		If DBExist = False Then
			LogColor($"${DB.DbType} database not existed!"$, COLOR_RED)
			CreateDatabase
			Return
		End If
		LogColor($"${DB.DbType} database existed!"$, COLOR_BLUE)
		'AddUsersTable
		If UsePool(DB.DbType) Then
			DB.InitPool
		End If
	Catch
		LogError(LastException.Message)
		LogColor("Error checking database!", COLOR_RED)
		Log("Application is terminated.")
		ExitApplication
	End Try
End Sub

Private Sub UsePool (Name As String) As Boolean
	Dim DbArray() As String = Array As String(DB.MARIADB, DB.MYSQL)
	Return DbArray.As(List).IndexOf(Name) > -1
End Sub

' Create Database Tables and Populate Data
Private Sub CreateDatabase
	LogColor("Creating database...", COLOR_BLUE)
	If UsePool(DB.DbType) Then
		Wait For (DB.CreateDatabaseAsync) Complete (Success As Boolean)
	Else
		Dim Success As Boolean = DB.InitializeSQLite
	End If
	If Not(Success) Then
		LogColor("Database creation failed!", COLOR_RED)
		Return
	End If
	
	LogColor("Creating tables...", COLOR_BLUE)
	If UsePool(DB.DbType) Then
		DB.InitPool
	End If

	DB.ShowExtraLogs = True
	DB.UseTimestamps = True
	DB.QueryAddToBatch = True
	
	DB.Table = "tbl_categories"
	DB.Columns.Add(DB.CreateColumn2(CreateMap("Name": "category_name", "Null": False)))
	DB.Create

	DB.Columns = Array("category_name")
	DB.Insert2(Array("Hardwares"))
	DB.Insert2(Array("Toys"))

	DB.Table = "tbl_products"
	DB.Columns.Add(DB.CreateColumn2(CreateMap("Name": "category_id", "Type": DB.INTEGER, "Null": False)))
	DB.Columns.Add(DB.CreateColumn2(CreateMap("Name": "product_code", "Length": "12", "Null": False)))
	DB.Columns.Add(DB.CreateColumn2(CreateMap("Name": "product_name", "Null": False)))
	DB.Columns.Add(DB.CreateColumn2(CreateMap("Name": "product_price", "Type": DB.DECIMAL, "Length": "10,2", "Null": False, "Default": "0.00")))
	DB.Columns.Add(DB.CreateColumn2(CreateMap("Name": "product_image", "Type": DB.BLOB)))
	'DB.Foreign("category_id", "id", "tbl_categories", "", "")
	DB.Foreign = "category_id"
	DB.References("tbl_categories", "id")
	DB.Create
	
	DB.Columns = Array("category_id", "product_code", "product_name", "product_price")
	DB.Insert2(Array(2, "T001", "Teddy Bear", 99.9))
	DB.Insert2(Array(1, "H001", "Hammer", 15.75))
	DB.Insert2(Array(2, "T002", "Optimus Prime", 1000))
	
	Wait For (DB.ExecuteBatchAsync) Complete (Success As Boolean)
	If Success Then
		LogColor("Database is created successfully!", COLOR_BLUE)
	Else
		LogColor("Database creation failed!", COLOR_RED)
	End If
	DB.Close
End Sub